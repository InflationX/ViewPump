apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.dokka-android'

dokka {
    reportUndocumented = false
    skipDeprecated = true
    jdkVersion = 7
    includes = ['Module.md']
    packageOptions {
        prefix = "io.github.inflationx.viewpump.internal"
        suppress = true
    }
    externalDocumentationLink {
        url = new URL("https://developer.android.com/reference/")
        packageListUrl =
            new URL("https://developer.android.com/reference/androidx/package-list")
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
    }
    lintOptions {
        // we don't always want to use the latest version of the support library
        disable 'GradleDependency'
        textOutput 'stdout'
        textReport System.getenv('CI') == 'true'
    }
    libraryVariants.all {
        // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
        it.generateBuildConfigProvider.configure {
            it.enabled = false
        }
    }
    variantFilter { variant ->
        if (variant.buildType.name == 'debug') {
            variant.setIgnore(true)
        }
    }
}

dependencies {
    compileOnly 'androidx.appcompat:appcompat:1.0.2'
    api 'org.jetbrains.kotlin:kotlin-stdlib:1.3.21'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:2.9.0'
    testImplementation 'org.mockito:mockito-core:2.15.0'

    testImplementation 'androidx.annotation:annotation:1.0.2'
    testImplementation 'androidx.test:runner:1.1.1'
}

apply plugin: 'com.vanniktech.maven.publish'
