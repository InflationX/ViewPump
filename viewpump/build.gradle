import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    alias(libs.plugins.agp.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.dokka)
    alias(libs.plugins.mavenPublish)
}

tasks.withType(DokkaTask.class).configureEach {
    dokkaSourceSets.configureEach {
        reportUndocumented = false
        skipDeprecated = true
        packageOptions {
            prefix = "io.github.inflationx.viewpump.internal"
            suppress = true
        }
        // AndroidX and Android docs are automatically added by the Dokka plugin.
    }
    includes = ['Module.md']
}

mavenPublishing {
    publishToMavenCentral("DEFAULT", true)
    signAllPublications()
}

android {
    compileSdk 33
    namespace = "io.github.inflationx.viewpump"

    defaultConfig {
        minSdk 14
    }

    lint {
        // we don't always want to use the latest version of the support library
        disable 'GradleDependency'
        textReport System.getenv('CI') == 'true'
    }

    buildFeatures {
        buildConfig = false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

androidComponents {
    beforeVariants(selector().all()) { variant ->
        variant.enabled = variant.buildType == 'release'
    }
}

tasks.withType(KotlinCompile.class).configureEach {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_1_8
    }
}

dependencies {
    compileOnly libs.androidx.appcompat

    testImplementation libs.junit
    testImplementation libs.assertj
    testImplementation libs.mockito

    testImplementation libs.androidx.annotation
    testImplementation libs.androidx.test.runner
}
